// SPDX-License-Identifier: LGPL-3.0-only
//
// This file is provided WITHOUT ANY WARRANTY;
// without even the implied warranty of MERCHANTABILITY
// or FITNESS FOR A PARTICULAR PURPOSE.
pragma solidity 0.8.28;

import { IE3Program } from "../interfaces/IE3Program.sol";
import { IBracken } from "../interfaces/IBracken.sol";

/// @dev Test-only E3 program with controls used to exercise failure and reentrancy paths.
contract MockE3ProgramHarness is IE3Program {
    error InvalidParams(bytes e3ProgramParams, bytes computeProviderParams);
    error E3AlreadyInitialized();
    error InvalidInput();

    bytes32 public constant ENCRYPTION_SCHEME_ID = keccak256("fhe.rs:BFV");

    IBracken public bracken;
    bool public reenterPlaintextPublication;
    bytes public reentrantPlaintext;
    bytes public reentrantProof;

    mapping(uint256 e3Id => bytes32 paramsHash) public paramsHashes;
    mapping(uint256 e3Id => bytes32 commitment)
        public expectedCiphertextCommitments;

    function setBracken(IBracken _bracken) external {
        bracken = _bracken;
    }

    function setExpectedCiphertextCommitment(
        uint256 e3Id,
        bytes32 commitment
    ) external {
        expectedCiphertextCommitments[e3Id] = commitment;
    }

    function setReentrantPlaintextPublication(
        bytes calldata plaintext,
        bytes calldata proof
    ) external {
        reenterPlaintextPublication = true;
        reentrantPlaintext = plaintext;
        reentrantProof = proof;
    }

    function validate(
        uint256 e3Id,
        uint256,
        bytes calldata e3ProgramParams,
        bytes calldata computeProviderParams,
        bytes calldata
    ) external returns (bytes32) {
        require(
            computeProviderParams.length == 32,
            InvalidParams(e3ProgramParams, computeProviderParams)
        );

        require(paramsHashes[e3Id] == bytes32(0), E3AlreadyInitialized());
        paramsHashes[e3Id] = keccak256(e3ProgramParams);
        return ENCRYPTION_SCHEME_ID;
    }

    function publishInput(uint256 e3Id, bytes memory data) external {
        _publishInput(e3Id, data, keccak256(data));
    }

    function publishInputWithCommitment(
        uint256 e3Id,
        bytes memory data,
        bytes32 ciphertextCommitment
    ) external {
        _publishInput(e3Id, data, ciphertextCommitment);
    }

    function _publishInput(
        uint256 e3Id,
        bytes memory data,
        bytes32 ciphertextCommitment
    ) internal {
        if (data.length == 3) revert InvalidInput();
        if (address(bracken) != address(0)) {
            bracken.publishCiphertextOutput(
                e3Id,
                data,
                ciphertextCommitment,
                data
            );
        }
    }

    function verify(
        uint256 e3Id,
        bytes32,
        bytes32 ciphertextCommitment,
        bytes memory data
    ) external returns (bool success) {
        bytes32 expected = expectedCiphertextCommitments[e3Id];
        if (expected != bytes32(0) && ciphertextCommitment != expected) {
            return false;
        }
        if (reenterPlaintextPublication) {
            bracken.publishPlaintextOutput(
                e3Id,
                reentrantPlaintext,
                reentrantProof
            );
        }
        return data.length > 0;
    }
}
